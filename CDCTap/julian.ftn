      INTEGER FUNCTION MMDDYY(YYDDD)
      IMPLICIT INTEGER (A-Z)
C 
C         MMDDYY-  CONVERT JULIAN DATE TO MM/DD/YY
C 
C         ENTRY-   YYDDD = JULIAN DATE IN DISPLAY CODE, RJ
C 
C         EXIT-    IF LEGAL DATE SUPPLIED, FUNCTION VALUE = 
C                    MM/DD/YY, LJBF.
C                  IF ILLEGAL DATE SUPPLIED, FUNCTION VALUE = 
C                    < YYDDD>, LJBF.
C 
C         CALLS-   IDXB            DISPLAY CODE TO BINARY 
C                  ICDD            BINARY TO DECIMAL DISPLAY CODE 
C                  PFDMPER         ABORT WITH MESSAGE 
C 
C         METHOD-  STRAIGHT-FORWARD.
C 
      DIMENSION MDAYS(12) 
      DATA MDAYS /31,28,31,30,31,30,31,31,30,31,30,31/
C 
C 
C         DECODE JULIAN DATE INTO BINARY
C 
      DDD=YYDDD.AND.777777B 
      YY=SHIFT(YYDDD,-18).AND.7777B 
C 
      DDD=IDXB(DDD) 
      YY=IDXB(YY) 
C 
      IF((DDD.LT.0).OR.(YY.LT.0)) GO TO 9999
C 
C         SET UP FOR LEAP YEAR
C 
      MDAYS(2)=28 
      MAXDDD=365
      IF((YY.AND.3).NE.0) GO TO 50
C 
      MDAYS(2)=29 
      MAXDDD=366
50    CONTINUE
C 
      IF(DDD.GT.MAXDDD) GO TO 9999
C 
C         DETERMINE MM/DD/YY
C 
      DD=DDD
      DO 100 MM=1,12
      IF((DD-MDAYS(MM)).LE.0) GO TO 200 
      DD=DD-MDAYS(MM) 
100   CONTINUE
      CALL PFDMPER("JULIAN DAY TOO BIG")
C 
200   CONTINUE
      MM=ICDD(100+MM).AND.7777B 
      DD=ICDD(100+DD).AND.7777B 
      YY=ICDD(100+YY).AND.7777B 
C 
      MMDDYY=SHIFT(MM,48).OR.SHIFT(1R/,42).OR.SHIFT(DD,30)
     +  .OR.SHIFT(1R/,24).OR.SHIFT(YY,12).OR.(2R  ) 
C 
      RETURN
C 
C         ILLEGAL JULIAN DATE SUPPLIED - RETURN THE JULIAN
C         DATE TO THE CALLER INSTEAD OF THE MM/DD/YY DATE.
C 
9999  CONTINUE
      MMDDYY=SHIFT((YYDDD.AND.77 7777 7777B),18).OR.2L< .OR.3R> 
      RETURN
C 
      END 
